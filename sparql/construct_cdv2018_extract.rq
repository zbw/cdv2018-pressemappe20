# Extract subset of PM20 for CdV2018
#
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
PREFIX gndo: <http://d-nb.info/standards/elementset/gnd#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX schema: <http://schema.org/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
#
construct {
  ?pm20 ?p ?o .
  ?pm20 zbwext:wdIdentifier ?wdId ;
    foaf:page ?page .
  ?pm20 schema:member ?member .
  ?member ?memberProp ?memberValue .
  ?pm20 zbwext:activity ?activity .
  ?activity ?actProp ?actValue .
  ?pm20 ?companyRelation ?otherCompany .
  ?otherCompany ?comProp ?comValue .
}
where {
  values ( ?companyRelation ) {
    ( schema:parentOrganization )
    ( schema:subOrganization )
    ( gndo:preceedingCorporateBody )
    ( gndo:succeedingCorporateBody )
  }
  # only person and company folders
  ?pm20 a ?class .
  filter(?class in (zbwext:PersonFolder, zbwext:CompanyFolder))
  ?pm20 dcterms:identifier ?id .
  filter(?id in ("pe/005823", "co/041389")) .
  #
  # only folders with freely viewable documents
  ?pm20 zbwext:freeDocCount ?docCount .
  filter(?docCount > 0)
  #
  # all available attributes
  ?pm20 ?p ?o .
  #
  # roles by organization members
  optional {
    ?pm20 schema:member ?member .
    ?member ?memberProp ?memberValue .
  }
  #
  # activities of a person
  optional {
    ?pm20 zbwext:activity ?activity .
    ?activity ?actProp ?actValue .
  }
  #
  # relations of a company
  optional {
    ?pm20 ?companyRelation ?otherCompany .
    ?otherCompany ?comProp ?comValue .
  }
  #
  # information from Wikidata
  ?pm20 dcterms:identifier ?pm20Id .
  optional {
    graph <http://zbw.eu/beta/wikidata/ng> {
      ?wd wdt:P4293 ?pm20Id .
      bind(strafter(str(?wd), str(wd:)) as ?wdId)
      #
      # German Wikipedia page
      optional {
        ?page schema:about ?wd ;
              schema:isPartOf <hattps://de.wikipedia.org/> .
      }
    }
  }
}
